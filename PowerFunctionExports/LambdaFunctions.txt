/*------------------------------------------------------------------------------------------------------------------
      Formula Name:   AppendTwoColumns
      Category:       Joins and data shapes
      Autohor:        Charl Potgieter
      Description:        Appends 2 columns, one below the other
------------------------------------------------------------------------------------------------------------------/*
=LAMBDA(range1,range2,
  LET(
          RowsRng1, ROWS(range1),
          RowsTotal, ROWS(range1) + ROWS(range2),
          Seq,SEQUENCE(RowsTotal),
          IF(
              Seq<=RowsRng1, INDEX(range1, Seq),
              INDEX(range2, Seq-RowsRng1)
             )
         )
)


/*------------------------------------------------------------------------------------------------------------------
      Formula Name:   StackArray
      Category:       Joins and data shapes
      Autohor:        Charl Potgieter
      Description:        Converts an x by y element array into a single column by stackign all the columns on top of each other
------------------------------------------------------------------------------------------------------------------/*
=LAMBDA(ArrayToStack,
     LET(
           ColCount, COLUMNS(ArrayToStack),
           RowCount, ROWS(ArrayToStack),
           CellCount, ColCount * RowCount,
           Seq, SEQUENCE(CellCount),
           RowIndex, MOD(Seq-1,RowCount)+1,
           ColIndex, ROUNDUP(Seq/RowCount, 0),
           ErrorValue, 1/0,
           ReturnValue, INDEX(ArrayToStack, RowIndex, ColIndex),
           ArrayInclErrors, IF(ReturnValue = "", ErrorValue, ReturnValue),
           FILTER(ArrayInclErrors, NOT(ISERROR(ArrayInclErrors)))
        )
)


/*------------------------------------------------------------------------------------------------------------------
      Formula Name:   SplitText
      Category:       Text
      Autohor:        ExcelJet
      Description:        https://exceljet.net/formula/lambda-split-text-to-array
------------------------------------------------------------------------------------------------------------------/*
=LAMBDA(text,delim,
      TRANSPOSE(
           FILTERXML(
                "<x><y>" & SUBSTITUTE(text,delim,"</y><y>") & "</y></x>",
                 "//y"
             )
       )
)


/*------------------------------------------------------------------------------------------------------------------
      Formula Name:   MonthEndsBetweenDates
      Category:       Date and time
      Autohor:        Charl Potgieter
      Description:        Returns an array of month ends between start and end dates
------------------------------------------------------------------------------------------------------------------/*
=LAMBDA(DateStart,DateEnd,
      LET(
           MonthsBetween, (YEAR(DateEnd) * 12 + MONTH(DateEnd))  - (YEAR(DateStart) * 12 + MONTH(DateStart)) +1,
           Seq, SEQUENCE(MonthsBetween),
           EOMONTH(DateStart, Seq-1)
      )
)


